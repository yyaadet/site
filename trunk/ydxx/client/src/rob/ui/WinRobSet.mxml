<?xml version="1.0" encoding="utf-8"?>
<GameWindow xmlns="utils.components.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="370" height="254" creationComplete="init();" xmlns:ns1="utils.components.item.*">
	
	<mx:Script>
		<![CDATA[
			import net.tcp.TcpManager;
			import net.tcp.model.wubao.TcpRob;
			import utils.WinManager;
			import images.AssetsManager;
			import utils.PubUnit;
			import general.model.General;
			import utils.GameManager;
			import wubao.model.WuBao;
			import wubao.model.User;
			
			private var _robUser:User = null;
			
			private var gameWubao:WuBao = null;
			
			[Bindable]
			private var generalList:Array = [];
			
			private var resSort:Array = [];
			
			private function init():void
			{
				setTitle("掠夺设定");
				helpID = 20;
			}
			
			override public function refresh():void
			{
				gameWubao = GameManager.gameMgr.wubao;
				
				var i:int = 0;
				for (i = 1; i <= 6; i++)
				{
					this["res" + i.toString()].clear();
					setResIcon(i, this["bagItem" + i.toString()]);
				}
				
				resSort.length = 0;
			}
			
			//1--钱  2--粮  3--木  4--铁  5--皮  6--马
			private function setResIcon(type:int, bagItem:BagItem):void
			{
				var bmp:Bitmap = null;
				var class1:Class = null;
				var tip:String = "";
				
				if (type == 1)
				{
					class1 = AssetsManager.assetsMgr.RES_MONEY;
					tip = "钱币";
				}
				else if (type == 2)
				{
					class1 = AssetsManager.assetsMgr.RES_FOOD;
					tip = "粮食";
				}
				else if (type == 3)
				{
					class1 = AssetsManager.assetsMgr.RES_WOOD;
					tip = "木料";
				}
				else if (type == 4)
				{
					class1 = AssetsManager.assetsMgr.RES_IRON;
					tip = "矿石";
				}
				else if (type == 5)
				{
					class1 = AssetsManager.assetsMgr.RES_SKIN;
					tip = "皮革";
				}
				else if (type == 6)
				{
					class1 = AssetsManager.assetsMgr.RES_HORSE;
					tip = "马匹";
				}
					
				bmp = new class1();
				bagItem.imageURL = bmp;
				bagItem.toolTip = tip;
				bagItem.num = 1;
			}
			
			private function resClick(evt:MouseEvent):void
			{
				var bagItem:BagItem = evt.target as BagItem;
				if (bagItem == null) return;
				
				if (!bagItem.isFill()) return;
				bagItem.clear();
				
				var type:int = 0;
				type = int(bagItem.id.substr(bagItem.id.length - 1, 1));
				
				moveDown(type);
				resSort[type] = 0;
			}
			
			private function selClick(evt:MouseEvent):void
			{
				var bagItem:BagItem = evt.target as BagItem;
				if (bagItem == null) return;
				
				if (!bagItem.isFill()) return;
				bagItem.clear();
				
				var type:int = 0;
				type = int(bagItem.id.substr(bagItem.id.length - 1, 1));
				moveUp(type);
			}
			
			private function moveUp(type:int):void
			{
				var resID:String = "";
				var i:int = 0;
				var bagItem:BagItem = null;
				
				for (i = 1; i <= 6; i++)
				{
					resID = "res" + i.toString();
					bagItem = this[resID] as BagItem;
					if (bagItem == null) continue;
					if (!bagItem.isFill())
					{
						setResIcon(type, this[resID]);
						resSort[i] = type; 
						return;
					}
				}
			}
			
			//1--钱  2--粮  3--木  4--铁  5--皮  6--马
			//1 粮食；2 木头；3 铁；4 马；5 皮革；6 钱；
			private function tranResType(type:int):int
			{
				var ret:int = 0;
				
				if (type == 1)
					ret = 6;
				else if (type == 2)
					ret = 1;
				else if (type == 3)
					ret = 2;
				else if (type == 4)
					ret = 3;
				else if (type == 5)
					ret = 5;
				else if (type == 6)
					ret = 4;
				
				return ret;
			}
			
			private function moveDown(type:int):void
			{
				var resID:String = "bagItem" + resSort[type].toString();
				setResIcon(resSort[type], this[resID]);
			}
			
			private function autoSel():void
			{
				var i:int = 0;
				var bagItem:BagItem = null;
				for (i = 1; i <= 6; i++)
				{
					bagItem = this["bagItem" + i.toString()] as BagItem;
					if (bagItem == null) continue;
					if (!bagItem.isFill()) continue;
					bagItem.clear();
					moveUp(i);
				}
			}
			
			private function startRob():void
			{
				var str:String = "";
				
				var i:int = 0;
				var selRes:Boolean = false;
				for (i = 1; i <= 6; i++)
				{
					if (resSort[i] > 0)
						selRes = true;
				}
				
				if (!selRes)
				{
					str = "请选择掠夺的资源";
					WinManager.winMgr.showResult(str);
					return;
				}
				
				var tcpRob:TcpRob = new TcpRob();
				tcpRob.userID = robUser.uniqID;
				
				for (i = 1; i <= 6; i++)
				{
					tcpRob["res" + i.toString() + "ID"] = getSelResID(i);
				}   
				
				TcpManager.tcpMgr.sendRob(tcpRob);
			}
			
			private function getSelResID(index:int):int
			{
				var i:int = 0;
				var bagItem:BagItem = null;
				var count:int = 0;
				
				for (i = 1; i <= 6; i++)
				{
					bagItem = this["res" + i.toString()] as BagItem;
					if (bagItem != null && bagItem.isFill())
					{
						count ++;
						if (count == index)
							return tranResType(resSort[i]);
					}
				}
				
				return 0;
			}
			
			public function get robUser():User
			{
				return _robUser;
			}
			
			public function set robUser(param:User):void
			{
				_robUser = param;
			}
			
			
		]]>
	</mx:Script>
	<mx:Canvas x="10" y="34" width="350" height="208" styleName="lowCanvas">
		<mx:Label x="10" y="10" text="掠夺资源的优先级：" height="20"/>
		<mx:Canvas x="10" y="30" width="330" height="50" click="resClick(event);">
			<ns1:BagItem id="res1" x="1" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="res2" x="57" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="res3" x="113" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="res4" x="169" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="res5" x="225" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="res6" x="281" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
		</mx:Canvas>
		
		<mx:Label x="10" y="90" text="点击图标设定掠夺资源的优先级" height="20"/>
		<mx:Canvas x="10" y="110" width="330" height="50" click="selClick(event);">
			<ns1:BagItem id="bagItem1" x="1" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="bagItem2" x="57" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="bagItem3" x="113" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="bagItem4" x="169" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="bagItem5" x="225" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
			<ns1:BagItem id="bagItem6" x="281" y="1" useHandCursor="true" buttonMode="true" showNum="false">
			</ns1:BagItem>
		</mx:Canvas>
		<GameButton label="开始掠夺" click="startRob();" x="276" y="168"/>
	</mx:Canvas>
	

</GameWindow>
