<?xml version="1.0" encoding="utf-8"?>
<GameWindow xmlns="utils.components.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="500" height="342" xmlns:ns1="utils.components.*" creationComplete="init();">
	
	<mx:Script>
		<![CDATA[
			import city.model.City;
			import net.tcp.TcpManager;
			import net.tcp.model.wubao.TcpVisit;
			import utils.WinManager;
			import wubao.model.Dignitie;
			import wubao.model.WuBao;
			import utils.GameManager;
			import general.model.General;
			
			private var gameWubao:WuBao = null;
			private var gameGeneral:General = null;
			
			private var _generalID:int = 0;
			
			private function init():void
			{
				helpID = 6;
				setTitle("武将信息");
			}
			
			override public function refresh():void
			{
				gameGeneral = GameManager.gameMgr.generalList.getObjByID(generalID) as General;
				generalInfo.gameGeneral = gameGeneral;
				
				if (gameGeneral.initYear > GameManager.gameMgr.gameYear)
				{
					btnGive.enabled = false;
					btnVisit.enabled = false;
				}
				else
				{
					btnGive.enabled = true;
					btnVisit.enabled = true;
				}
			}
			
			private function give():void
			{
				gameWubao = GameManager.gameMgr.wubao;
				var dig:Dignitie = GameManager.gameMgr.digList.getObjByID(gameWubao.digID) as Dignitie;
				var generalMax:int = 0;
				if (dig != null)
					generalMax = dig.generalMax;
				
				var generalNum:int = gameWubao.generalList.length;
				var str:String = "";
				if (generalNum >= generalMax)
				{
					str = "武将数目已达上限";
					WinManager.winMgr.showResult(str);
					return;
				}
				
				if (gameGeneral.userID == gameWubao.userID)
				{
					str = "无法结交自己的武将";
					WinManager.winMgr.showResult(str);
					return;
				}
				
				if (gameGeneral.userID > 0)
				{
					str = "只能结交没有主公的武将";
					WinManager.winMgr.showResult(str);
					return;
				}
				
				WinManager.winMgr.showGive(gameGeneral.uniqID);
			}
			
			private function visit():void
			{
				gameWubao = GameManager.gameMgr.wubao;
				var str:String = "";
				var gameCity:City = GameManager.gameMgr.cityList.getObjByID(gameGeneral.placeID) as City;
				if (gameCity.sphereID <= 0 || gameCity.sphereID != gameWubao.sphereID)
				{
					str = "只能拜访本势力所占有的城池中的武将";
					WinManager.winMgr.showResult(str);
					return;
				}
				
				if (gameGeneral.userID == gameWubao.userID)
				{
					str = "无法拜访自己的武将";
					WinManager.winMgr.showResult(str);
					return;
				}
				
				var tcpVisit:TcpVisit = new TcpVisit();
				tcpVisit.wbID = gameWubao.uniqID;
				tcpVisit.generalID = gameGeneral.uniqID;
				TcpManager.tcpMgr.sendVisitGeneral(tcpVisit);
			}
			
			public function get generalID():int
			{
				return _generalID;
			}
			
			public function set generalID(param:int):void
			{
				_generalID = param;
			}
			
		]]>
	</mx:Script>
	<mx:Canvas  x="10" y="34" width="480" height="264" styleName="lowCanvas">
		<ns1:GeneralInfo id="generalInfo" x="8" y="8">
		</ns1:GeneralInfo>
	</mx:Canvas>
	<ns1:GameButton id="btnVisit" label="拜  访" width="64" height="24" x="182" click="visit();" y="306"/>
	<ns1:GameButton id="btnGive" label="结  交" width="64" height="24" x="254" click="give();" y="306"/>
</GameWindow>
