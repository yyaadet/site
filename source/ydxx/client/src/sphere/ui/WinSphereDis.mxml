<?xml version="1.0" encoding="utf-8"?>
<GameWindow xmlns="utils.components.*" xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:ns1="utils.components.*" width="720" height="604" creationComplete="init();">
	
	<mx:Script>
		<![CDATA[
			import map.MapUtil;
			import utils.FormatText;
			import wubao.model.WuBao;
			import sphere.list.SphereList;
			import sphere.model.Sphere;
			import map.list.CityPointList;
			import map.model.CityPoint;
			import mx.core.ScrollPolicy;
		
			import utils.PubUnit;
			import city.model.City;
			import utils.GameManager;
			import city.list.CityList;
			
			private var gameWubao:WuBao = null;
			private var cityID:int = 0;
			private var cityList:CityList = null;
			private var sphereList:SphereList = null;
			private var cityPointList:CityPointList = new CityPointList();
			
			private function init():void
			{
				setTitle("势力分布");
				helpID = 10;
			}
			
			override public function refresh():void
			{
				gameWubao = GameManager.gameMgr.wubao;
				cityList = GameManager.gameMgr.cityList;
				sphereList = GameManager.gameMgr.sphereList;
				sphereList.randomColor();
				
				var i:int = 0;
				var gameCity:City = null;
				var gameSphere:Sphere = null;
				var len:int = cityList.length;
				var r:int = 0;
				var pX:int = 0;
				var pY:int = 0;
				var pColor:uint = 0;
				var point:Point = null;
				
				for (i = 0; i < len; i++)
				{
					gameCity = cityList.getObjByIndex(i) as City;
					
					r = 2 * gameCity.level + 4;
					pX = int(gameCity.mapX * 0.14 - r / 2);
					pY = int(gameCity.mapY * 0.14 - r / 2);
					
					var selfID:int = 0;
					var targetID:int = 0;
					
					selfID = gameWubao.sphereID;
					targetID = gameCity.sphereID;
					
					if (selfID == targetID && selfID != 0)
						pColor = PubUnit.WHITE;
					else
						pColor = PubUnit.BLACK;
						
					drawCity(r, pX, pY, pColor, gameCity);
					
					r = 2 * gameCity.level + 2;
					point = MapUtil.transCoor(gameCity.mapX, gameCity.mapY);
					pX = int(gameCity.mapX * 0.14 - r / 2);
					pY = int(gameCity.mapY * 0.14 - r / 2);
					
					if (gameCity.sphereID == 0)
						pColor = PubUnit.WHITE;
					else
					{
						var sphereID:int = gameCity.sphereID;
						gameSphere = sphereList.getObjByID(sphereID) as Sphere;
						if (gameSphere != null)
							pColor = gameSphere.mapColor;
					}
					
					drawCity(r, pX, pY, pColor, gameCity);
				}
			}
			
			private function drawCity(r:int, pX:int, pY:int, pColor:uint, gameCity:City):void
			{
				var cityPoint:CityPoint = new CityPoint();
				cityPoint.uniqID = gameCity.uniqID;
				cityPoint.gameCity = gameCity;
				cityPoint.pWidth = r;
				cityPoint.pHeight = r;
				cityPoint.x = pX;
				cityPoint.y = pY;
				cityPoint.pColor = pColor;
				cityPoint.draw();
				mapSel.addChild(cityPoint);
				cityPointList.add(cityPoint);
				cityPoint.addEventListener(MouseEvent.MOUSE_OVER, onOver);
			} 
			
			private function onOver(evt:MouseEvent):void
			{
				var cityPoint:CityPoint = evt.target as CityPoint;
				if (cityPoint == null) return;
				
				var gameCity:City = cityPoint.gameCity;
				if (gameCity == null) return;
				
				var tip:String = "";
				
				tip += FormatText.packegText("城池 ") + FormatText.packegText(gameCity.cityName, FormatText.GREEN) + "\n";
				tip += FormatText.packegText("所在 ") + FormatText.packegText(gameCity.upName, FormatText.GREEN) + "\n";
				
				if (gameCity.sphereName != "")
					tip += FormatText.packegText("势力 ") + FormatText.packegText(gameCity.sphereName, FormatText.GREEN) + "\n";
				
				if (gameCity.isAlloted == 1)
					tip += FormatText.packegText("玩家 ") + FormatText.packegText(gameCity.userList.length.toString(), FormatText.GREEN);
					
				cityPoint.toolTip = tip;
			}
			
		]]>
	</mx:Script>
	
	<mx:Canvas id="mapSel" x="8" y="34" width="704" height="560" backgroundImage="@Embed(source='../../images/assets/map/city_sel.jpg')" styleName="lowCanvas" backgroundAlpha="1.0">
	</mx:Canvas>
	
</GameWindow>
