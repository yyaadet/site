<?xml version="1.0" encoding="utf-8"?>
<GameWindow xmlns="utils.components.*" xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:ns1="utils.components.*" width="820" height="540" creationComplete="init();">
	
	<mx:Script>
		<![CDATA[
			import sphere.list.DipList;
			import utils.WinManager;
			import net.tcp.TcpManager;
			import net.tcp.model.wubao.TcpTrans;
			import general.model.General;
			import city.ui.events.SendEvent;
			import city.ui.menu.MenuSend;
			import map.MapUtil;
			import utils.FormatText;
			import wubao.model.WuBao;
			import sphere.list.SphereList;
			import sphere.model.Sphere;
			import map.list.CityPointList;
			import map.model.CityPoint;
			import mx.core.ScrollPolicy;
		
			import utils.PubUnit;
			import city.model.City;
			import utils.GameManager;
			import city.list.CityList;
			
			private var _generalID:int = 0;
			
			private var gameWubao:WuBao = null;
			private var gameGeneral:General = null;
			private var selCity:City = null;
			private var cityList:CityList = null;
			private var cityPointList:CityPointList = new CityPointList();
			private var dipList:DipList = null;
			
			private function init():void
			{
				setTitle("城池选择");
			}
			
			override public function refresh():void
			{
				gameWubao = GameManager.gameMgr.wubao;
				gameGeneral = GameManager.gameMgr.generalList.getObjByID(generalID) as General;
				cityList = GameManager.gameMgr.cityList;
				dipList = GameManager.gameMgr.dipList;
				
				var i:int = 0;
				var gameCity:City = null;
				var len:int = cityList.length;
				var r:int = 0;
				var pX:int = 0;
				var pY:int = 0;
				var pColor:uint = 0;
				var point:Point = null;
				
				for (i = 0; i < len; i++)
				{
					gameCity = cityList.getObjByIndex(i) as City;
					if (gameCity.sphereID == gameWubao.sphereID || dipList.isAlli(gameWubao.sphereID, gameCity.sphereID))
					{
						r = 2 * gameCity.level + 4;
						point = MapUtil.transCoor(gameCity.mapX, gameCity.mapY);
						pX = int(point.x / 2.5 - r / 2);
						pY = int(point.y / 2.5 - r / 2);
						
						if (gameWubao.cityID == gameCity.uniqID)
							pColor = PubUnit.WHITE;
						else if (dipList.isAlli(gameWubao.sphereID, gameCity.sphereID))
							pColor = PubUnit.BLUE;
						else
							pColor = PubUnit.GREEN;
							
						drawCity(r, pX, pY, pColor, gameCity);
					}
				}
			}
			
			private function drawCity(r:int, pX:int, pY:int, pColor:uint, gameCity:City):void
			{
				var cityPoint:CityPoint = new CityPoint();
				cityPoint.uniqID = gameCity.uniqID;
				cityPoint.gameCity = gameCity;
				cityPoint.pWidth = r;
				cityPoint.pHeight = r;
				cityPoint.x = pX;
				cityPoint.y = pY;
				cityPoint.pColor = pColor;
				cityPoint.draw();
				mapSel.addChild(cityPoint);
				cityPointList.add(cityPoint);
				cityPoint.addEventListener(MouseEvent.MOUSE_OVER, onOver);
				cityPoint.addEventListener(MouseEvent.CLICK, cityClick);
			} 
			
			private function onOver(evt:MouseEvent):void
			{
				var cityPoint:CityPoint = evt.target as CityPoint;
				if (cityPoint == null) return;
				
				var gameCity:City = cityPoint.gameCity;
				if (gameCity == null) return;
				
				var tip:String = "";
				
				tip += FormatText.packegText("城池 ") + FormatText.packegText(gameCity.cityName, FormatText.GREEN) + "\n";
				tip += FormatText.packegText("所在 ") + FormatText.packegText(gameCity.upName, FormatText.GREEN) + "\n";
				
				if (gameWubao.sphereID == gameCity.sphereID)
					tip += FormatText.packegText("势力 ") + FormatText.packegText(gameCity.sphereName, FormatText.GREEN);
				else if (dipList.isAlli(gameWubao.sphereID, gameCity.sphereID))
					tip += FormatText.packegText("势力 ") + FormatText.packegText(gameCity.sphereName, FormatText.GREEN);
				
				cityPoint.toolTip = tip;
			}
			
			private function cityClick(evt:MouseEvent):void
			{
				var cityPoint:CityPoint = evt.target as CityPoint;
				if (cityPoint == null) return;
				
				var gameCity:City = cityPoint.gameCity;
				if (gameCity == null) return;
				
				selCity = gameCity;
				WinManager.winMgr.showTranSel(selCity, gameGeneral);
			}
			
			public function get generalID():int
			{
				return _generalID;
			}
			
			public function set generalID(param:int):void
			{
				_generalID = param;
			}
			
		]]>
	</mx:Script>
	
	<mx:Canvas id="mapSel" x="8" y="32" width="804" height="500" styleName="lowCanvas" backgroundAlpha="1.0">
	</mx:Canvas>
	
</GameWindow>
